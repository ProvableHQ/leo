/// Common

// Declared in common/assignee.rs
assignee = { self_keyword_or_identifier ~ access_assignee* }

// Declared in files/file.rs
file = { SOI ~ definition* ~ EOI }

// Declared in definitions/definition.rs
definition = {
    definition_annotated
    | import
    | circuit
    | function
    | deprecated
}

// Declared in definitions/deprecated.rs
deprecated = {
    test_function
}

// Declared in definitions/annotated_definition.rs
definition_annotated = { annotation ~ definition}

// Declared in common/identifier.rs
identifier = @{ ((!protected_name ~ ASCII_ALPHA) | (protected_name ~ (ASCII_ALPHANUMERIC | "_"))) ~ (ASCII_ALPHANUMERIC | "_")* }
protected_name = {
    "as"
    | "circuit"
    | "const"
    | "console"
    | "else"
    | "for"
    | "function"
    | "if"
    | "import"
    | input_keyword
    | "in"
    | "let"
    | "mut"
    | "return"
    | self_keyword
    | "static"
    | "string"
    | "test"
    | type_data
    | type_self
    | value_boolean
}

// Declared in common/self_keyword.rs
self_keyword = { "self" }

// Declared in common/mut_self_keyword.rs
mut_self_keyword = { mutable ~ self_keyword }

// Declared in common/self_keyword_or_identifier.rs
self_keyword_or_identifier = {
    self_keyword
    | identifier
}

// Declared in common/keyword_or_identifier.rs
keyword_or_identifier = {
    input_keyword
    | type_self
    | identifier
}

// Declared in common/line_end.rs
LINE_END = { ";" }

// Declared in common/mutable.rs
mutable = { "mut " }

// Declared in common/range.rs
range_operator = { ".." }
range = { expression? ~ range_operator ~ expression? }

// Declared in common/range_or_expression.rs
range_or_expression = { range | expression }

// Declared in common/spread.rs
spread = { "..." ~ expression }

// Declared in common/spread_or_expression.rs
spread_or_expression = { spread | expression }

// Declared in common/static_.rs
static_ = { "static " }

// Declared in common/variable_name.rs

variable_name = {mutable? ~ identifier}
variable_name_tuple = _{"(" ~ variable_name ~ ("," ~ variable_name)+ ~ ")"}

// Declared in common/variables.rs
variables = { ( variable_name_tuple | variable_name ) ~ (":" ~ type_ )? }

// Declared in common/declare.rs
declare = { let_ | const_ }
const_ = { "const " }
let_ = { "let " }

/// Operations

// Declared in operations/unary_operation.rs
operation_unary = {
    operation_not
    | operation_negate
}
operation_not = { "!" }
operation_negate = { "-" }

// Declared in operations/binary_operation.rs
operation_and = { "&&" }
operation_or = { "||" }
operation_eq = { "==" }
operation_ne = { "!=" }
operation_ge = { ">=" }
operation_gt = { ">" }
operation_le = { "<=" }
operation_lt = { "<" }
operation_add = { "+" }
operation_sub = { "-" }
operation_mul = { "*" }
operation_div = { "/" }
operation_pow = { "**" }

operation_compare = _{
    operation_eq | operation_ne |
    operation_ge | operation_gt |
    operation_le | operation_lt
}
operation_binary = _{
    operation_compare | operation_and | operation_or |
    operation_add | operation_sub | operation_pow | operation_mul | operation_div
}

// Declared in operations/assign_operation.rs
operation_assign = {
    assign | operation_add_assign | operation_sub_assign |
    operation_mul_assign | operation_div_assign | operation_pow_assign
}
assign = { "=" }
operation_add_assign = { "+=" }
operation_sub_assign = { "-=" }
operation_mul_assign = { "*=" }
operation_div_assign = { "/=" }
operation_pow_assign = { "**=" }

/// Types

// Declared in types/type_.rs
type_ = { type_self | type_tuple | type_array | type_data | type_circuit }

// Declared in types/integer_type.rs
type_integer = {
    type_integer_signed
    | type_integer_unsigned
}

// Declared in types/unsigned_integer_type.rs
type_u8 = { "u8" }
type_u16 = { "u16" }
type_u32 = { "u32" }
type_u64 = { "u64" }
type_u128 = { "u128" }

type_integer_unsigned = {
    type_u8
    | type_u16
    | type_u32
    | type_u64
    | type_u128
}

// Declared in types/signed_integer_type.rs
type_i8 = { "i8" }
type_i16 = { "i16" }
type_i32 = { "i32" }
type_i64 = { "i64" }
type_i128 = { "i128" }

type_integer_signed = {
    type_i8
    | type_i16
    | type_i32
    | type_i64
    | type_i128
}

// Declared in types/field_type.rs
type_field = { "field" }

// Declared in types/group_type.rs
type_group = { "group" }

// Declared in types/boolean_type.rs
type_boolean = { "bool" }

// Declared in types/address_type.rs
type_address = { "address" }

// Declared in types/data_type.rs
type_data = {
    type_address
    | type_boolean
    | type_field
    | type_group
    | type_integer
}

// Declared in types/self_type.rs
type_self = { "Self" }

// Declared in types/self_type.rs
type_circuit = { identifier }

// Declared in types/array_type.rs
type_array = { "[" ~ type_ ~ ";" ~ array_dimensions ~ "]" }

// Declared in types/array_dimensions.rs
array_dimensions = {
    dimension_single
    | dimension_multiple
}

// Declared in types/array_dimensions.rs
dimension_single = {
    number_positive
}

// Declared in types/array_dimensions.rs
dimension_multiple = { "(" ~ number_positive ~ ("," ~ number_positive)* ~ ")"}

type_tuple = { "(" ~ (type_ ~ ("," ~ type_)+ ~ ","?)? ~ ")" }

/// Values

// Declared in values/value.rs
value = {
    value_address
    | value_boolean
    | value_field
    | value_group
    | value_integer
    | value_number // must be last as a catch all
}

// Declared in values/number_value.rs
value_number = { number_negative | number_positive }

// Declared in values/number_negative.rs
number_negative = @{ "-" ~ ASCII_DIGIT+ }

// Declared in values/number_positive.rs
number_positive = @{ ASCII_DIGIT+ }

// Declared in values/integer_value.rs
value_integer = { value_integer_signed | value_integer_unsigned}

// Declared in values/signed_integer_value.rs
value_integer_signed = ${ value_number ~ type_integer_signed }

// Declared in values/unsigned_integer_value.rs
value_integer_unsigned = ${ number_positive ~ type_integer_unsigned }

// Declared in values/boolean_value.rs
value_boolean = { "true" | "false" }

// Declared in values/field_value.rs
value_field = ${ value_number ~ type_field }

// Declared in values/group_value.rs
value_group = ${ group_single_or_tuple ~ type_group }
group_single_or_tuple = {value_number | group_tuple}
group_tuple = !{"(" ~ group_coordinate ~ "," ~ group_coordinate ~ ")"}

// Declared in values/group_coordinate.rs
group_coordinate = {
    value_number
    | sign_high
    | sign_low
    | inferred
}

sign_high = @{"+"}
sign_low = @{"-"}
inferred = @{"_"}

// Declared in values/address.rs
address = @{ "aleo1" ~ (LOWERCASE_LETTER | ASCII_DIGIT){58} }

// Declared in values/address_value.rs
value_address = ${ type_address ~ "(" ~ address ~ ")" }

/// Access

// Declared in access/access.rs
access = { access_array | access_tuple | access_call | access_member | access_static_member }

// Declared in access/self_access.rs
self_access = { access_member | access_static_member }

// Declared in access/array_access.rs
access_array = !{ "[" ~ range_or_expression ~ "]" }

// Declared in access/tuple_access.rs
access_tuple = ${ "." ~ number_positive }

// Declared in access/assignee_access.rs
access_assignee = { access_array | access_tuple | access_member }

// Declared in access/call_access.rs
access_call = !{ "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }

// Declared in access/member_access.rs
access_member = ${ "." ~ identifier }

// Declared in access/static_member_access.rs
access_static_member = ${ "::" ~ identifier }

/// Circuits

// Declared in circuits/circuit_definition.rs
circuit = { "circuit " ~ identifier ~ "{" ~ circuit_member* ~ "}" }

// Declared in circuits/circuit_variable.rs
circuit_variable = { identifier ~ ":" ~ expression }

// Declared in circuits/circuit_implied_variable.rs
circuit_implied_variable = {
    circuit_variable
    | identifier
}

// Declared in circuits/circuit_variable_definition.rs
circuit_variable_definition = { identifier ~ ":" ~ type_ ~ ","?}

// Declared in circuits/circuit_member.rs
circuit_member = { function | circuit_variable_definition}

/// Conditionals

expression_conditional = { "if " ~ expression ~ "? " ~ expression ~ ": " ~ expression}

/// Expressions

expression_term = {
    value
    | ("(" ~ expression ~ ")")
    | expression_tuple
    | expression_conditional
    | expression_array_initializer
    | expression_array_inline
    | expression_circuit_inline
    | expression_unary
    | self_expression_postfix
    | expression_postfix
    | identifier
}


// Declared in expressions/expression.rs
expression = { expression_term ~ (operation_binary ~ expression_term)* }

// Declared in expressions/expression_tuple.rs
expression_tuple = { "(" ~ (expression ~ ("," ~ expression)+)? ~ ")" }

// Declared in expressions/array_initializer_expression.rs
expression_array_initializer = { "[" ~ expression ~ ";" ~ array_dimensions ~ "]" }

// Declared in expressions/array_inline_expression.rs
expression_array_inline = { "[" ~ inline_array_inner ~ "]"}
inline_array_inner = _{(spread_or_expression ~ ("," ~ spread_or_expression)*)?}

// Declared in expressions/circuit_inline_expression.rs
expression_circuit_inline = { circuit_name ~ "{" ~ circuit_variable_list ~ "}" }

// Declared in expressions/circuit_inline_expression.rs
circuit_name = {
    type_self
    | identifier
}

// Declared in expressions/circuit_inline_expression.rs
circuit_variable_list = _{ (circuit_implied_variable ~ ("," ~ circuit_implied_variable)*)? ~ ","? }

// Declared in expressions/unary_expression.rs
expression_unary = { operation_unary ~ expression_term }

// Declared in expressions/postfix_expression.rs
expression_postfix = ${ keyword_or_identifier ~ access+ }

// Declared in expressions/self_postfix_expression.rs
self_expression_postfix = ${ self_keyword ~ self_access ~ access* }

/// Statements

// Declared in statements/statement.rs
statement = {
    (statement_return
    | statement_conditional
    | statement_for
    | console_function_call
    | statement_definition
    | statement_assign
    | statement_expression
    | block
    )
}


// Declared in statements/assign_statement.rs
statement_assign = { assignee ~ operation_assign ~ expression ~ LINE_END }

block = { "{" ~ statement* ~ "}" }

// Declared in statements/conditional_statement.rs
statement_conditional = {"if " ~ expression ~ block ~ ("else " ~ conditional_nested_or_end_statement)?}
conditional_nested_or_end_statement = { statement_conditional | block }

// Declared in statements/definition_statement.rs
statement_definition = { declare ~ variables ~ "=" ~ expression ~ LINE_END}

// Declared in statements/expression_statement.rs
statement_expression = { expression ~ LINE_END }

// Decalred in functions/test_function.rs
test_function = { "test" ~ function }

// Declared in statements/for_statement.rs
statement_for = { "for " ~ identifier ~ "in " ~ expression ~ ".." ~ expression ~ block }

// Declared in statements/return_statement.rs
statement_return = { "return " ~ expression}

/// Functions

// Declared in functions/function.rs
function = { "function " ~ identifier ~ input_tuple  ~ ("->" ~ type_)? ~ block }

// Declared in functions/input/function_input.rs
function_input = { const_? ~ mutable? ~ identifier ~ ":" ~ type_ }

// Declared in functions/input/input_keyword.rs
input_keyword = { "input" }

// Declared in functions/input/input.rs
input = {
    input_keyword
    | self_keyword
    | mut_self_keyword
    | function_input
}
input_tuple = _{ "(" ~ (input ~ ("," ~ input)* ~ ","?)? ~ ")"}


/// Imports

// Declared in imports/import.rs
import = { "import " ~ package_or_packages ~ LINE_END}

// Declared in imports/package_name.rs
package_name = @{ (ASCII_ALPHA_LOWER | ASCII_DIGIT)+ ~ ( "-" ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT)+)* }

// Declared in imports/package_or_packages.rs
package_or_packages = {
    packages
    | package
}
// Declared in imports/package.rs
package = { package_name ~ "." ~ package_access }
// Declared in imports/packages.rs
packages = { package_name ~ "." ~ multiple_package_access }

// Declared in imports/package_access
package_access = {
    star
    | packages 
    | package // subpackage
    | import_symbol
}

multiple_package_access = _{ "(" ~ package_access ~ ("," ~ package_access)* ~ ","? ~ ")"}

// Declared in imports/star.rs
star = {"*"}

// Declared in imports/import_symbol.rs
import_symbol = { identifier ~ ("as " ~ identifier)? }

/// Utilities

COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/") | ("//" ~ (!NEWLINE ~ ANY)*) }
WHITESPACE = _{ (" " | "\t" | NEWLINE) ~ (NEWLINE)* } // pest implicit whitespace keyword

/// Console Functions

// Declared in console/console_function_call.rs
console_function_call = ${console_keyword ~ "." ~ console_function ~ LINE_END}

// Declared in console/console_keyword.rs
console_keyword = {"console"}

// Declared in console/console_function.rs
console_function = {
    console_assert
    | console_debug
    | console_error
    | console_log
}

// Declared in console/console_assert.rs
console_assert = !{"assert(" ~ expression ~ ")"}

// Declared in console/console_debug.rs
console_debug = !{"debug(" ~ formatted_string? ~ ")"}

// Declared in console/console_error.rs
console_error = !{"error(" ~ formatted_string? ~ ")"}

// Declared in console/console_log.rs
console_log = !{"log(" ~ formatted_string? ~ ")"}

// Declared in console/formatted_string.rs
formatted_string = {
    "\""
    ~ (!"\"" ~ (formatted_container | ANY))*
    ~ "\""
    ~ ("," ~ expression)*
}

// Declared in console/formatted_container.rs
formatted_container = { "{" ~ "}"}

/// Annotations

// Declared in annotations/annotation.rs
annotation = ${annotation_symbol ~ annotation_name ~ annotation_arguments? }

// Declared in annotations/annotation_symbol.rs
annotation_symbol = ${"@"}

// Declared in annotations/annotation_name.rs
annotation_name = {
    context // deprecated
    | test
}

// Declared in annotations/annotation_name.rs
context = {"context"}
test = {"test"}

// Declared in annotations/annotation_argument.rs
annotation_arguments = !{"(" ~ annotation_argument ~ ("," ~ annotation_argument)* ~ ","? ~  NEWLINE* ~ ")"}

annotation_argument = @{ (ASCII_ALPHANUMERIC | "_")+ }
